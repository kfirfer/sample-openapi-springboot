buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.2.1"

    }
}

plugins {
    id "org.openapi.generator" version "4.2.1"
}
apply plugin: 'base'

group = 'com.kfirfer'


subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'com.kfirfer'
    version = '0.0.2'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.fork = true

    project.buildDir = "${rootProject.buildDir}/${project.name}"
    sourceSets {
        main {
            java {
                srcDir "${project.buildDir}/src/main/java"
            }
        }
    }

    // This is just for generating the source jar
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

ext.appName = 'user-service'
ext.apiPackage = 'com.kfirfer.userservice.api'
ext.modelPackage = 'com.kfirfer.userservice.model'
ext.swaggerFile = "${rootDir}/api/swagger.yml"
ext.templateDirectoryVar = "${rootDir}/template"

project("${rootProject.appName}-server") {

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web:2.2.1.RELEASE'
        implementation 'io.swagger:swagger-annotations:1.5.24'
        implementation 'com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4'
        implementation 'org.openapitools:jackson-databind-nullable:0.2.0'
        implementation 'io.swagger.core.v3:swagger-annotations:2.1.0'
    }
    openApiGenerate {
        generatorName = "spring"
        inputSpec = rootProject.swaggerFile.toString()
        outputDir = project.buildDir.toString()
        apiPackage = rootProject.apiPackage
        modelPackage = rootProject.modelPackage

        templateDir = rootProject.templateDirectoryVar

        configOptions = [
                dateLibrary           : "java7",
                'skipDefaultInterface': 'true',
                'useTags'             : 'true',
                'interfaceOnly'       : 'true',
                'useBeanValidation'   : 'true',
                'delegatePattern'     : 'false'
        ]
    }
}



